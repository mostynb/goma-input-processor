# Copyright 2014 The Goma Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

roots = "roots"

if (os != "win") {
  genc = "//client/genc.py"

  action("gen_roots_certs") {
    script = genc
    sources = [ "$roots.pem" ]
    outputs = [
      "$target_gen_dir/$roots.c",
      "$target_gen_dir/$roots.h",
    ]
    args = [
      "--prefix=certs_",
      "--out-dir",
      rebase_path("$target_gen_dir", root_build_dir),
      rebase_path("$roots.pem", root_build_dir),
    ]
  }

  config("certs_config") {
    include_dirs = [ "$target_gen_dir" ]
  }
  static_library("certs") {
    sources = [
      "$target_gen_dir/$roots.c",
      "$target_gen_dir/$roots.h",
    ]
    deps = [ ":gen_roots_certs" ]
    public_configs = [ ":certs_config" ]
  }
} else {  # os == "win"
  config("certs_resource_config") {
    include_dirs = [ "." ]
  }

  # This needs to be source_set (instead of static_library), otherwise
  # openssl_engine will fail to read certs.rc.
  source_set("certs_resource") {
    sources = [
      "certs.rc",
      "certs_resource.h",
    ]
    public_configs = [ ":certs_resource_config" ]
    data = [ "$roots.pem" ]
  }
}
